" -----------------------------------------------------------------------
" General Settings
" -----------------------------------------------------------------------

" Don't make vim vi-compatibile.
set nocompatible

" Enable syntax highlighting.
syntax on

if has('autocmd')
	filetype plugin indent on
	"         [1]    [2]  [3]
	"
	" 1. Enable loading of plugin files.
	" 2. Enable loading of indent file.
	" 3. Enable file type detection. 
endif


" Copy indent to the new line.
set autoindent

" Better line wrapping.
set breakindent

" Allow `backspace` in insert mode.
set backspace=indent
set backspace+=eol
set backspace+=start

" Set directory for backup files.
set backupdir=~/.vim/backups

" Don't create backups for certain files.
set backupskip=/tmp/*
set backupskip+=/private/tmp/*

" Use the system clipboard as the default register.
set clipboard=unnamed
if has('unnamedplus')
	set clipboard+=unnamedplus
endif

" When making a change, don't redisplay the line, and instead, put a
" `$` sign at the end of the changed text.
set cpoptions+=$

" When included, as much as possible of the last line in a window will be
" displayed. When not included, a last line that doesn't fit is replaced
" with "@" lines.
set display+=lastline

" Force the cursor onto a new line after 80 characters
set textwidth=80

" Highlight certain column(s).
set colorcolumn=73

" Highlight the current line.
set cursorline

" Set directory for swap files.
set directory=~/.vim/swaps

" Use UTF-8 without BOM.
set encoding=utf-8 nobomb

" Use UTF-8.
set fileencoding=utf-8

" Increase command line history.
set history=5000

" Enable search highlighting.
set hlsearch

" Ignore case in search patterns.
set ignorecase

" Highlight search pattern as it is being typed.
set incsearch

" Always show the status line.
set laststatus=2

" Do not redraw the screen while executing macros, registers and other 
" commands that have not been typed.
set lazyredraw

"  Use custom symbols to represent invisible characters.
set listchars=tab:→\
set listchars+=trail:·
set listchars+=eol:↴
set listchars+=nbsp:_

" Enable extended regexp.
set magic

" Enable mouse in all modes.
set mouse=a

" Hide mouse pointer while typing.
set mousehide

" Disable error bells.
set noerrorbells

" When using the join command, only insert a single space 
" after a `.`, `?`, or `!`.
set nojoinspaces

" Kept the cursor on the same column.
set nostartofline

" Show line number.
set number

" Increase the minimal number of columns used for the `line number`.
set numberwidth=5

" Report the number of lines changed.
set report=0

" Show cursor position.
set ruler

" When scrolling, keep the cursor 5 lines below the top and 5 lines
" above the bottom of the screen.
set scrolloff=5

" Avoid all the hit-enter prompts.
set shortmess=aAItW

" Show the command being typed.
set showcmd

" Show current mode.
set showmode

" Show matching parens, brackets, etc.
set showmatch

" Set the spell-check language.
set spelllang=en_gb

" Override `ignorecase` option if the search pattern 
" contains uppercase characters.
set smartcase

" Limit syntax highlighting (this avoids the very slow redrawing
" when files contain long lines).
set synmaxcol=2500

" Set global <TAB> settings
" http://vimcasts.org/e/2
set tabstop=4
set softtabstop=4
set shiftwidth=4

" Indent using tab characters (Indent using spaces set expandtab).
set noexpandtab

" Enable fast terminal connection.
set ttyfast

" Set directory for undo files.
set undodir=~/.vim/undos

" Automatically save undo history.
set undofile

" Allow cursor to be anywhere.
set virtualedit=all

" Disable beeping and window flashing
" https://vim.wikia.com/wiki/Disable_beeping
set visualbell
set noerrorbells
set t_vb=

" Enable enhanced command-line completion (by hitting <TAB> in
" command mode, Vim will show the possible matches just above the
" command line with the first match highlighted).
set wildmenu

" Don’t offer to open certain files/directories
set wildignore+=*.gif,*.jpg
set wildignore+=*.png
set wildignore+=node_modules/*

" Allow windows to be squashed.
set winminheight=0


" -----------------------------------------------------------------------
" Plugins
" -----------------------------------------------------------------------

" Use Vundle to manage the vim plugins.
" https://github.com/gmarik/Vundle.vim

" -----------------------------------------------------------------------

" Disable file type detection
" (this is required by Vundle).

filetype off

" -----------------------------------------------------------------------

" Include Vundle in the runtime path.

set runtimepath+=~/.vim/plugins/Vundle.vim

" Initialize Vundle and specify the path
" where the plugins should be installed .

call vundle#begin('~/.vim/plugins')

	" Let Vundle manage Vundle (this is important!)
	Plugin 'VundleVim/Vundle.vim'

	Plugin 'altercation/vim-colors-solarized'
	Plugin 'ap/vim-css-color'
	Plugin 'chrisbra/unicode.vim'
	Plugin 'editorconfig/editorconfig-vim'
	Plugin 'godlygeek/tabular'
	Plugin 'isRuslan/vim-es6'
	Plugin 'jelera/vim-javascript-syntax'
	Plugin 'kien/ctrlp.vim'
	Plugin 'marijnh/tern_for_vim'
	Plugin 'mattn/emmet-vim'
	Plugin 'mattn/webapi-vim'
	Plugin 'mhinz/vim-signify'
	Plugin 'nathanaelkane/vim-indent-guides'
	Plugin 'raimondi/delimitmate'
	Plugin 'scrooloose/nerdtree'
	Plugin 'scrooloose/syntastic'
	Plugin 'sheerun/vim-polyglot'
	Plugin 'shougo/neocomplcache.vim'
	Plugin 'shougo/neosnippet'
	Plugin 'shougo/neosnippet-snippets'
	Plugin 'sindresorhus/vim-xo'
	Plugin 'suan/vim-instant-markdown'
	Plugin 'shutnik/jshint2.vim'
	Plugin 'tomtom/tcomment_vim'
	Plugin 'tpope/vim-commentary'
	Plugin 'tpope/vim-fugitive'
	Plugin 'tpope/vim-repeat'
	Plugin 'tpope/vim-surround'

call vundle#end()

" -----------------------------------------------------------------------

" Re-enable file type detection
" (disabling it was required by Vundle).

filetype on


" -----------------------------------------------------------------------
" Plugins - Emmet
" -----------------------------------------------------------------------

" Redefine trigger key for Emmet.
" http://docs.emmet.io/cheat-sheet/

let g:user_emmet_leader_key='<C-E>'

" Load custom Emmet snippets.
" http://docs.emmet.io/customization/snippets/

let g:user_emmet_settings = webapi#json#decode(join(readfile(expand('~/.vim/snippets/emmet.json')), "\n"))


" -----------------------------------------------------------------------
" Plugins - Indent Guides
" -----------------------------------------------------------------------

" Set custom indent colors.
" https://github.com/nathanaelkane/vim-indent-guides#setting-custom-indent-colors

let g:indent_guides_auto_colors = 0

autocmd VimEnter,Colorscheme * :hi IndentGuidesOdd
		\ guibg=#00323D
		\ ctermbg=Magenta

autocmd VimEnter,Colorscheme * :hi IndentGuidesEven
		\ guibg=#073642
		\ ctermbg=DarkMagenta


" -----------------------------------------------------------------------
" Plugins - Instant Markdown
" -----------------------------------------------------------------------

" Do not automatically launch the preview
" window when the markdown file is open.
" https://github.com/suan/vim-instant-markdown#ginstant_markdown_autostart

let g:instant_markdown_autostart = 0


" -----------------------------------------------------------------------
" Plugins - Markdown
" -----------------------------------------------------------------------

" Disable Folding.
" https://github.com/plasticboy/vim-markdown#disable-folding

let g:vim_markdown_folding_disabled = 1


" -----------------------------------------------------------------------
" Plugins - NeoComplCache
" -----------------------------------------------------------------------

" Enable `neocomplcache` by default.
" https://github.com/Shougo/neocomplcache.vim#installation

let g:neocomplcache_enable_at_startup = 1

" Make `Tab` autocomplete.

inoremap <expr><TAB> pumvisible() ? "\<C-n>" : "\<TAB>"

" Make `Shift+Tab` insert `Tab` character.

inoremap <S-TAB> <C-V><TAB>


" -----------------------------------------------------------------------
" Plugins - Signify
" -----------------------------------------------------------------------

" Disable Signify by default.
" https://github.com/mhinz/vim-signify

let g:signify_disable_by_default = 1


" -----------------------------------------------------------------------
" Plugins - Syntastic
" -----------------------------------------------------------------------

" Inform Syntastic which checkers to use based on file types.
" https://github.com/scrooloose/syntastic#faq

let g:syntastic_javascript_checkers = ['xo']

" Disable syntax checking by default.

let g:syntastic_mode_map = {
	\ 'active_filetypes': [],
	\ 'mode': 'passive',
	\ 'passive_filetypes': []
\}


" -----------------------------------------------------------------------
" Helper Functions
" -----------------------------------------------------------------------

function! GetGitBranchName()

	let branchName = ''

	if exists('g:loaded_fugitive')
		let branchName = '[' . fugitive#head() . ']'
	endif

	return branchName

endfunction

" -----------------------------------------------------------------------

function! StripTrailingWhitespaces()

	" Save last search and cursor position.

	let searchHistory = @/
	let cursorLine = line('.')
	let cursorColumn = col('.')

	" Strip trailing whitespaces.

	%s/\s\+$//e

	" Restore previous search history and cursor position.

	let @/ = searchHistory
	call cursor(cursorLine, cursorColumn)

endfunction

" -----------------------------------------------------------------------

function! ToggleLimits()

	" [51,73]
	"  - git commit message
	"     http://tbaggery.com/2008/04/19/a-note-about-git-commit-messages.html
	" [81]
	"  - general use

	if (&colorcolumn == '73')
		set colorcolumn+=51,81
	else
		set colorcolumn-=51,81
	endif

endfunction

" -----------------------------------------------------------------------

function! ToggleRelativeLineNumbers()
	if (&relativenumber == 1)
		set number
		set norelativenumber
	else
		set number
		set relativenumber
	endif
endfunction


" -----------------------------------------------------------------------
" Automatic Commands
" -----------------------------------------------------------------------

if has('autocmd')

	" Autocommand Groups.
	" http://learnvimscriptthehardway.stevelosh.com/chapters/14.html

	" -------------------------------------------------------------------

	" Automatically reload the configurations from
	" the `~/.vimrc` file whenever they are changed.

	augroup auto_reload_vim_configs

		autocmd!
		autocmd BufWritePost vimrc source $MYVIMRC

	augroup END

	" -------------------------------------------------------------------

	" Use relative line numbers.
	" http://jeffkreeftmeijer.com/2012/relative-line-numbers-in-vim-for-super-fast-movement/

	augroup relative_line_numbers

		autocmd!

		" Automatically switch to absolute
		" line numbers when Vim loses focus.

		autocmd FocusLost * :set number norelativenumber

		" Automatically switch to relative
		" line numbers when vim gains focus.

		autocmd FocusGained * :set relativenumber

		" Automatically switch to absolute
		" line numbers when vim is in insert mode.

		autocmd InsertEnter * :set number norelativenumber

		" Automatically switch to relative
		" line numbers when vim is in normal mode.

		autocmd InsertLeave * :set relativenumber

	augroup END

	" -------------------------------------------------------------------

	" Automatically strip the trailing
	" whitespaces when files are saved.

	augroup strip_trailing_whitespaces

		" List of file types that use the trailing whitespaces:
		"
		" * Markdown
		"   https://daringfireball.net/projects/markdown/syntax#block

		let excludedFileTypes = [
			\ 'markdown',
			\ 'mkd.markdown'
		\]

		" Only strip the trailing whitespaces if
		" the file type is not in the excluded list.

		autocmd!
		autocmd BufWritePre * if index(excludedFileTypes, &ft) < 0 | :call StripTrailingWhitespaces()

	augroup END

endif


" -----------------------------------------------------------------------
" Color Scheme
" -----------------------------------------------------------------------

" Enable full-color support.
set t_Co=256

" Use colors that look good on a dark background.
set background=dark

" Set custom configurations for when the
" Solarized theme is used from vim's Terminal mode.
"
" http://ethanschoonover.com/solarized/vim-colors-solarized#advanced-configuration

if !has('gui_running')
	let g:solarized_contrast = 'normal'
	let g:solarized_termcolors = 256
	let g:solarized_termtrans = 1
	let g:solarized_visibility = 'low'
endif

" Use custom color scheme.
colorscheme solarized


" -----------------------------------------------------------------------
" Key Mappings
" -----------------------------------------------------------------------

" Use a different mapleader (default is '\').

let mapleader = ','

" -----------------------------------------------------------------------

" Make `Y` work from the cursor to end of line.

nmap Y y$

" -----------------------------------------------------------------------

" [,* ] Search and replace the word under the cursor.

nmap <leader>* :%s/\<<C-r><C-w>\>//<Left>

" -----------------------------------------------------------------------

" [,cc] Toggle code comments.
" https://github.com/tomtom/tcomment_vim

map <leader>cc :TComment<CR>

" -----------------------------------------------------------------------

" [,cr] Calculate result.
" http://vimcasts.org/e/56

nmap <leader>cr 0yt=A<C-r>=<C-r>"<CR><Esc>

" -----------------------------------------------------------------------

" clear highlights by hitting [esc]
" or by hitting [enter] in normal mode

nnoremap <CR> :noh<CR><CR>

" -----------------------------------------------------------------------

" [,cs] Clear search.

map <leader>cs <Esc>:noh<CR>

" -----------------------------------------------------------------------

" [,gd] Toggle Git differences.

map <leader>gd :SignifyToggle<CR>

" -----------------------------------------------------------------------

" [,h ] JSHint the code.
" https://github.com/Shutnik/jshint2.vim

nmap <leader>h :JSHint<CR>

" -----------------------------------------------------------------------

" [,l ] Toggle `set list`.

nmap <leader>l :set list!<CR>

" -----------------------------------------------------------------------

" [,n ] Toggle `set relativenumber`.

nmap <leader>n :call ToggleRelativeLineNumbers()<CR>

" -----------------------------------------------------------------------

" [,sc] Toggle spellcheck.

map <leader>sc :setlocal spell!<CR>

" -----------------------------------------------------------------------

" [,ss] Strip trailing whitespace.

nmap <leader>ss :call StripTrailingWhitespaces()<CR>

" -----------------------------------------------------------------------

" [,tn] Toggle NERDTree.

map <leader>t :NERDTreeToggle<CR>

" -----------------------------------------------------------------------

" [,ti] Toggle indent.

nmap <leader>ti <Plug>IndentGuidesToggle

" -----------------------------------------------------------------------

" [,tl] Toggle show limits.

nmap <leader>tl :call ToggleLimits()<CR>

" -----------------------------------------------------------------------

" [,ts] Toggle Syntastic.

nmap <leader>ts :SyntasticToggleMode \| w<CR>

" -----------------------------------------------------------------------

" [,v ] Make the opening of the `.vimrc` file easier.

nmap <leader>v :vsp $MYVIMRC<CR>

" -----------------------------------------------------------------------

" [,W ] Sudo write.

map <leader>W :w !sudo tee %<CR>


" -----------------------------------------------------------------------
" Status Line
" -----------------------------------------------------------------------

" Terminal types:
"
"  1. term  (normal terminals, e.g.: vt100, xterm)
"  2. cterm (color terminals, e.g.: MS-DOS console, color-xterm)
"  3. gui   (GUIs)

highlight ColorColumn
	\ term=NONE
	\ cterm=NONE  ctermbg=237    ctermfg=NONE
	\ gui=NONE    guibg=#073642  guifg=NONE

highlight CursorLine
	\ term=NONE
	\ cterm=NONE  ctermbg=235  ctermfg=NONE
	\ gui=NONE    guibg=#073642  guifg=NONE

highlight CursorLineNr
	\ term=bold
	\ cterm=bold  ctermbg=NONE   ctermfg=178
	\ gui=bold    guibg=#073642  guifg=Orange

highlight LineNr
	\ term=NONE
	\ cterm=NONE  ctermfg=241    ctermbg=NONE
	\ gui=NONE    guifg=#839497  guibg=#073642

highlight User1
	\ term=NONE
	\ cterm=NONE  ctermbg=237    ctermfg=Grey
	\ gui=NONE    guibg=#073642  guifg=#839496

set statusline=
set statusline+=%1*                           " User1 highlight
set statusline+=\ [%n]                        " Buffer number
set statusline+=\ %{GetGitBranchName()}       " Git branch name
set statusline+=\ [%f]                        " Path to the file
set statusline+=%m                            " Modified flag
set statusline+=%r                            " Readonly flag
set statusline+=%h                            " Help file flag
set statusline+=%w                            " Preview window flag
set statusline+=%y                            " File type
set statusline+=[
set statusline+=%{&ff}                        " File format
set statusline+=:
set statusline+=%{strlen(&fenc)?&fenc:'none'} " File encoding
set statusline+=]
set statusline+=%=                            " Left/Right separator
set statusline+=%c                            " File encoding
set statusline+=,
set statusline+=%l                            " Current line number
set statusline+=/
set statusline+=%L                            " Total number of lines
set statusline+=\ (%P)\                       " Percent through file

" Preview:
"
" [1] [master] [vim/vimrc][vim][unix:utf-8]            17,238/381 (59%)


" -----------------------------------------------------------------------
" Local Settings
" -----------------------------------------------------------------------

" Load local settings if they exist.
"
" [!] The following needs to remain at the end of this file in
"     order to allow any of the above settings to be overwritten
"     by the local ones.

if filereadable(glob("~/.vimrc.local"))
	source ~/.vimrc.local
endif
